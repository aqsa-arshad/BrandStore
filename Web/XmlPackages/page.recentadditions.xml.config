<?xml version="1.0" encoding="utf-8" ?>
<!-- ##################################################################################	-->
<!-- Copyright AspDotNetStorefront.com. All Rights Reserved.							-->
<!-- http://www.aspdotnetstorefront.com													-->
<!-- For details on this license please visit the product homepage at the URL above.	-->
<!-- THE ABOVE NOTICE MUST REMAIN INTACT.												-->
<!-- ##################################################################################	-->
<package displayname="Recent Additions" version="2.1" debug="false" allowengine="true">
	<query name="Products" rowElementName="Product">
		<sql>
			<![CDATA[
                exec aspdnsf_RecentAdditions @since, @return, @StoreID, @FilterProduct
            ]]>
		</sql>
		<queryparam paramname="@since"          paramtype="appconfig" requestparamname="RecentAdditionsNumDays" sqlDataType="int" defvalue="180"  validationpattern="^\d{1,10}$" />
		<queryparam paramname="@return"         paramtype="appconfig" requestparamname="RecentAdditionsN"       sqlDataType="int" defvalue="100"  validationpattern="^\d{1,3}$" />
		<queryparam   paramname="@StoreID"	    paramtype="runtime"   requestparamname="StoreID"								sqlDataType="int"	defvalue="1"    validationpattern="" />
		<queryparam paramname="@FilterProduct"  paramtype="runtime"   requestparamname="FilterProduct"	        sqlDataType="bit"	defvalue="0"    validationpattern="" />
	</query>

	<PackageTransform>
		<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
			<xsl:output method="html" omit-xml-declaration="yes" />

			<xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
			<xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />

			<xsl:param name="ShowPicsHeight">
				<xsl:value-of select="aspdnsf:AppConfig('RecentAdditionsShowPicsHeight')"/>
			</xsl:param>
			<xsl:param name="ShowPics">
				<xsl:value-of select="aspdnsf:AppConfigBool('RecentAdditionsShowPics')"/>
			</xsl:param>
			<xsl:param name="ProductCount" select="count(/root/Products/Product)" />
			<xsl:variable name="ProductIdsOnPage">
				<xsl:for-each select="/root/Products/Product/ProductID">
					<xsl:value-of select="." />
					<xsl:if test="position() != last()">
						<xsl:text>,</xsl:text>
					</xsl:if>
				</xsl:for-each>
			</xsl:variable>
			<xsl:variable name="AvailablePromotions" select="aspdnsf:XmlPackageAsXml('availablePromotions.xml.config', concat('productIdList=', $ProductIdsOnPage))/root/Promotions" />

			<xsl:variable name="SubcatGridCols" select="4" />
			<xsl:variable name="GridItemCSSClassName">
				<xsl:choose>
					<xsl:when test="$SubcatGridCols = 1">full-width</xsl:when>
					<xsl:when test="$SubcatGridCols = 2">one-half</xsl:when>
					<xsl:when test="$SubcatGridCols = 3">one-third</xsl:when>
					<xsl:when test="$SubcatGridCols = 4">one-fourth</xsl:when>
					<xsl:when test="$SubcatGridCols = 5">one-fifth</xsl:when>
					<xsl:when test="$SubcatGridCols = 6">one-sixth</xsl:when>
					<xsl:otherwise>one-fourth</xsl:otherwise>
				</xsl:choose>
			</xsl:variable>

			<xsl:template match="/">
				<div class="page-wrap recent-additions-page">
					<h1 class="recent-additions-page-header">
						<xsl:value-of select="aspdnsf:StringResource('recentadditions.aspx.1')" disable-output-escaping="yes" />
					</h1>
					<xsl:choose>
						<xsl:when test="$ProductCount &gt; 0">
							<div class="page-row products-row recent-additions-products">
								<xsl:apply-templates select="/root/Products/Product" />
							</div>
						</xsl:when>
						<xsl:otherwise>
							<p class="no-recent-additions-prompt">
								<xsl:value-of select="aspdnsf:StrFormatStringresource('recentadditions.aspx.5',aspdnsf:AppConfig('RecentAdditionsNumDays'),'')" disable-output-escaping="yes" />
							</p>
						</xsl:otherwise>
					</xsl:choose>
				</div>
			</xsl:template>

			<xsl:template match="Product">
				<xsl:variable name="pName" select="aspdnsf:GetMLValue(ProductName)"></xsl:variable>
				<xsl:variable name="pSEAltText" select="aspdnsf:GetMLValue(SEAltText)"></xsl:variable>
				<xsl:variable name="ImageUrl" select="aspdnsf:ImageUrl(ProductID, 'product', 'icon', 0)" />
				<xsl:variable name="AltText">
					<xsl:choose>
						<xsl:when test="$pSEAltText=''">
							<xsl:value-of select="$pName" />
						</xsl:when>
						<xsl:otherwise>
							<xsl:value-of select="$pSEAltText" />
						</xsl:otherwise>
					</xsl:choose>
				</xsl:variable>
				<xsl:variable name="productId" select="ProductID" />
				<xsl:variable name="isRecurring" select="HasRecurring" />

				<!-- Start a new row if this one is full -->
				<xsl:if test="position() mod $SubcatGridCols = 1 and position() &gt; 1">
					<xsl:variable name="NewRowHtml">
						<xsl:text>&lt;/div&gt;&lt;div class="page-row grid-item-row"&gt;</xsl:text>
					</xsl:variable>
					<xsl:value-of select="$NewRowHtml" disable-output-escaping="yes" />
				</xsl:if>

				<!-- Assign the class determined above for the grid -->
				<div class="{$GridItemCSSClassName} product-grid-item">
					<div class="grid-item-inner">
						<div class="grid-item-image-wrap">
							<a href="{aspdnsf:ProductLink(ProductID, SEName, 0)}">
								<img src="{$ImageUrl}" alt="{$AltText}" class="grid-item-image product-image" />
							</a>
						</div>
						<div class="grid-item-name-wrap">
							<a href="{aspdnsf:ProductLink(ProductID, SEName, 0)}">
								<xsl:value-of select="$pName" disable-output-escaping="yes" />
								<xsl:if test="string-length(VariantName) &gt; 0">
									<xsl:text>&#32;-&#32;</xsl:text>
									<xsl:value-of select="aspdnsf:GetMLValue(VariantName)" disable-output-escaping="yes" />
								</xsl:if>
							</a>
						</div>
						<xsl:if test="aspdnsf:AppConfigBool('DisplayOutOfStockProducts') = 'true'">
							<div class="grid-item-stock-wrap">
								<xsl:value-of select="aspdnsf:DisplayProductStockHint(ProductID,'Entity')" disable-output-escaping="yes" />
							</div>
						</xsl:if>
						<xsl:if test="isRecurring = 'True'">
							<div class="grid-item-recurring-wrap">
								<xsl:value-of select="aspdnsf:StringResource('product.CallToAction.IsRecurring', $LocaleSetting)" disable-output-escaping="yes" />
							</div>
						</xsl:if>
						<!-- Run the Promotion template to display the call to action text for any auto-assigned promos that apply to this product -->
						<div class="grid-item-promotions-wrap">
							<xsl:apply-templates select="$AvailablePromotions/Promotion[ProductId=$productId]" />
						</div>
					</div>
				</div>

			</xsl:template>

			<xsl:template match="Promotion">
				<span class="promotion-call-to-action">
					<xsl:value-of select="CallToAction" disable-output-escaping="yes" />
				</span>
			</xsl:template>
		</xsl:stylesheet>
	</PackageTransform>
</package>
