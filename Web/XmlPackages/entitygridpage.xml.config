<?xml version="1.0" standalone="yes" ?>
<!-- ##################################################################################	-->
<!-- Copyright AspDotNetStorefront.com. All Rights Reserved.							-->
<!-- http://www.aspdotnetstorefront.com													-->
<!-- For details on this license please visit the product homepage at the URL above.	-->
<!-- THE ABOVE NOTICE MUST REMAIN INTACT.												-->
<!-- ##################################################################################	-->
<package version="2.1" displayname="Entity Root Level Grid" debug="false" includeentityhelper="true">

  <!-- ###################################################################################################### -->
  <!-- Notes: expects param of Entity to be passed in by Runtime                                             -->
  <!-- ###################################################################################################### -->

  <PackageTransform>
    <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
      <xsl:output method="html" omit-xml-declaration="yes" />
      <xsl:variable name="EntityName">
        <xsl:choose>
          <xsl:when test="string-length(/root/Runtime/entity) &gt; 0">
            <xsl:value-of select="/root/Runtime/entity" />
          </xsl:when>
          <xsl:otherwise>
            <xsl:value-of select="/root/QueryString/entity" />
          </xsl:otherwise>
        </xsl:choose>
      </xsl:variable>
      <xsl:variable name="SubcatGridCols" select="aspdnsf:AppConfig(concat('Default_', $EntityName, 'PageSize'))" />
      <xsl:variable name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
      <xsl:variable name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />
      <xsl:variable name="ProductIdsOnPage">
        <xsl:for-each select="/root/EntityHelpers/*[name()=$EntityName]/Entity/EntityID">
          <xsl:value-of select="." />
          <xsl:if test="position() != last()">
            <xsl:text>,</xsl:text>
          </xsl:if>
        </xsl:for-each>
      </xsl:variable>
      <xsl:variable name="AvailablePromotions" select="aspdnsf:XmlPackageAsXml('availablePromotions.xml.config', concat('productIdList=', $ProductIdsOnPage))/root/Promotions" />
      <xsl:param name="EntityDisplayName">
        <xsl:value-of select="aspdnsf:StringResource(concat('AppConfig.', $EntityName, 'PromptPlural'), $LocaleSetting)" />
      </xsl:param>
      <xsl:variable name="GridItemCSSClassName">
        <xsl:choose>
          <xsl:when test="$SubcatGridCols = 1">full-width</xsl:when>
          <xsl:when test="$SubcatGridCols = 2">one-half</xsl:when>
          <xsl:when test="$SubcatGridCols = 3">one-third</xsl:when>
          <xsl:when test="$SubcatGridCols = 4">one-fourth</xsl:when>
          <xsl:when test="$SubcatGridCols = 5">one-fifth</xsl:when>
          <xsl:when test="$SubcatGridCols = 6">one-sixth</xsl:when>
          <xsl:otherwise>one-fourth</xsl:otherwise>
        </xsl:choose>
      </xsl:variable>

      <xsl:template match="/">
        <div class="page-wrap entity-page manufacturers-page">
          <h1>
            All <xsl:value-of select="$EntityDisplayName" />
          </h1>
          <div class="page-row sub-entity-row">
            <xsl:apply-templates select="/root/EntityHelpers/*[name()=$EntityName]/child::Entity" />
          </div>
        </div>
      </xsl:template>

      <xsl:template match="Entity">
        <xsl:variable name="Name" select="aspdnsf:GetMLValue(Name)" />
        <xsl:variable name="AltText">
          <xsl:choose>
            <xsl:when test="aspdnsf:GetMLValue(SEAltText)=''">
              <xsl:value-of select="$Name" />
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="aspdnsf:GetMLValue(SEAltText)" />
            </xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        <xsl:variable name="URL">
          <xsl:value-of select="aspdnsf:EntityLink(EntityID, SEName, $EntityName, 0, '')" />
        </xsl:variable>

        <xsl:if test="position() mod $SubcatGridCols = 1 and position() &gt; 1">
          <xsl:variable name="NewRowHtml">
            <xsl:text>&lt;/div&gt;&lt;div class="page-row  sub-entity-row"&gt;</xsl:text>
          </xsl:variable>
          <xsl:value-of select="$NewRowHtml" disable-output-escaping="yes" />
        </xsl:if>
        <div class="{$GridItemCSSClassName} sub-entity-grid-item grid-item">
          <div class="grid-item-inner">
            <div class="sub-entity-image-wrap grid-item-image-wrap">
              <a href="{$URL}" class="sub-entity-image-link">
                <img src="{aspdnsf:ImageUrl(EntityID, $EntityName, 'icon', 0)}" alt="{$AltText}" class="img-responsive img-thumbnail sub-entity-grid-image grid-item-image" />
              </a>
            </div>
            <div class="sub-entity-name-wrap grid-item-name-wrap">
              <a href="{$URL}" class="sub-entity-name-link">
                <xsl:value-of select="$Name" disable-output-escaping="yes"/>
              </a>
            </div>
          </div>
        </div>
      </xsl:template>

      <xsl:template match="Promotion">
        <div class="promotionCallToAction">
          <xsl:value-of select="CallToAction" disable-output-escaping="yes" />
        </div>
      </xsl:template>
      
    
  
  </xsl:stylesheet>
  </PackageTransform>

  <SearchEngineSettings>
    <SectionTitle actionType="transform">
      <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf">
        <xsl:output method="html" omit-xml-declaration="yes" />
        <xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
        <xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />
        <xsl:variable name="EntityName">
          <xsl:choose>
            <xsl:when test="string-length(/root/Runtime/entity) &gt; 0">
              <xsl:value-of select="/root/Runtime/entity" />
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="/root/QueryString/entity" />
            </xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        <xsl:param name="EntityDisplayName">
          <xsl:value-of select="aspdnsf:StringResource(concat('AppConfig.', $EntityName, 'PromptPlural'), $LocaleSetting)" />
        </xsl:param>
        <xsl:template match="/">
          All <xsl:value-of select="$EntityDisplayName" />
        </xsl:template>
      </xsl:stylesheet>
    </SectionTitle>
  </SearchEngineSettings>

</package>
