<?xml version="1.0" encoding="utf-8" standalone="yes" ?>
<!-- ##################################################################################	-->
<!-- Copyright AspDotNetStorefront.com. All Rights Reserved.							-->
<!-- http://www.aspdotnetstorefront.com													-->
<!-- For details on this license please visit the product homepage at the URL above.	-->
<!-- THE ABOVE NOTICE MUST REMAIN INTACT.												-->
<!-- ##################################################################################	-->

<package nopass2="false" nosystem="false" debug="false" includeentityhelper="true" allowengine="true">
    <PackageTransform>
        <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
            <xsl:output method="xml" omit-xml-declaration="no" indent="no" />
            
            <xsl:param name="entity" select="/root/QueryString/entityname"></xsl:param>
            <xsl:param name="ForParentEntityID" select="/root/QueryString/parentid"></xsl:param>
            <xsl:param name="custlocale" select="/root/System/LocaleSetting"></xsl:param>
            <xsl:param name="deflocale" select="/root/System/WebConfigLocaleSetting"></xsl:param>

            <xsl:template match="root">
                <xsl:param name="prefix">
                    <xsl:choose>
                        <xsl:when test="$entity='Category'"><xsl:value-of select="'c'" /></xsl:when>
                        <xsl:when test="$entity='Section'"><xsl:value-of select="'s'" /></xsl:when>
                        <xsl:when test="$entity='Manufacturer'"><xsl:value-of select="'m'" /></xsl:when>
                        <xsl:when test="$entity='Library'"><xsl:value-of select="'l'" /></xsl:when>
                    </xsl:choose>
                </xsl:param>
                
                <Nodes>
                    <xsl:apply-templates select="/root/EntityHelpers/*[name()=$entity]/descendant::Entity[ParentEntityID=$ForParentEntityID]" >
                        <xsl:with-param name="prefix" select="$prefix"/>
                    </xsl:apply-templates>

                    <!-- ADD NEW -->
                    <TreeViewNode>
                        <xsl:attribute name="Text">&lt;font color='red'&gt;Add <xsl:value-of select="$entity"/>&lt;/font&gt;</xsl:attribute>
                        <xsl:attribute name="NavigateUrl">entityEdit.aspx?entityparent=<xsl:value-of select="EntityID"/>&amp;entityname=<xsl:value-of select="$entity"/>&amp;entityid=0</xsl:attribute>
                    </TreeViewNode >

                    <!-- PRODUCTS -->
                    <TreeViewNode>
                        <xsl:attribute name="Text">
                            &lt;a target=entityBody href=entityProducts.aspx?entityname=<xsl:value-of select="$entity"/>&amp;EntityFilterID=<xsl:value-of select="$ForParentEntityID"/>&gt;
                            &lt;font color='green'&gt;Products&lt;/font&gt;
                            &lt;/a&gt;
                        </xsl:attribute>
                        <xsl:attribute name="ContentCallbackUrl">XmlEntityProducts.aspx?entityname=<xsl:value-of select="$entity"/>&amp;entityid=<xsl:value-of select="$ForParentEntityID"/></xsl:attribute>
                    </TreeViewNode>
                </Nodes>
            
            </xsl:template>

            <xsl:template match="Entity">
                <xsl:param name="prefix"></xsl:param>
                <xsl:param name="eName">
                    <xsl:choose>
                        <xsl:when test="count(Name/ml/locale[@name=$custlocale])!=0 and Name/ml/locale[@name=$custlocale] != ''">
                            <xsl:value-of select="Name/ml/locale[@name=$custlocale]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml/locale[@name=$deflocale])!=0 and Name/ml/locale[@name=$deflocale] != ''">
                            <xsl:value-of select="Name/ml/locale[@name=$deflocale]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml)=0">
                            <xsl:value-of select="Name"/>
                        </xsl:when>
                        <xsl:otherwise>[Not Set for this Locale]</xsl:otherwise>
                    </xsl:choose>
                </xsl:param>
            
                <TreeViewNode>
                    <xsl:attribute name="NavigateUrl">entityEdit.aspx?entityname=<xsl:value-of select="$entity"/>&amp;entityid=<xsl:value-of select="EntityID"/></xsl:attribute>
                    <xsl:attribute name="Text"><xsl:value-of select="aspdnsf:HtmlEncode($eName)"/></xsl:attribute>
                    <xsl:attribute name="ContentCallbackUrl">x-EntitySubmenu.aspx?entityname=<xsl:value-of select="$entity"/>&amp;parentid=<xsl:value-of select="EntityID"/></xsl:attribute>
                </TreeViewNode>
          </xsl:template>

        </xsl:stylesheet>
    </PackageTransform>
    <HTTPHeaders>
        <HTTPHeader headername="Content-Type" headervalue="text/xml" />
    </HTTPHeaders>
</package>
