<?xml version="1.0" standalone="yes" ?>
<!-- ##################################################################################	-->
<!-- Copyright AspDotNetStorefront.com. All Rights Reserved.							-->
<!-- http://www.aspdotnetstorefront.com													-->
<!-- For details on this license please visit the product homepage at the URL above.	-->
<!-- THE ABOVE NOTICE MUST REMAIN INTACT.												-->
<!-- ##################################################################################	-->
<package version="2.1" displayname="Entity List" debug="false" includeentityhelper="true" allowengine="true">
	<PackageTransform>
			<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
					<xsl:output method="html" omit-xml-declaration="yes" />

					<xsl:variable name="EntityName" select="aspdnsf:StrCapitalize(/root/QueryString/entitytype)" />
					<xsl:variable name="CurrentEntity" select="/root/EntityHelpers/*[name()=$EntityName]" />
					<xsl:template match="/">
						<h1>
							<xsl:value-of select="aspdnsf:StringResource(concat('AppConfig.',$EntityName, 'PromptPlural'))" disable-output-escaping="yes" />
						</h1>
						<xsl:choose>
							<xsl:when test="aspdnsf:AppConfigBool('ShowSubCatsInGrid') = 'true'">
								<div class="sub-entity-grid">
									<div class="row sub-entity-row">
										<xsl:apply-templates select="$CurrentEntity/Entity" mode="grid" />
									</div>
								</div>
							</xsl:when>
							<xsl:otherwise>
								<ul class="sub-entity-list">
									<xsl:apply-templates select="$CurrentEntity/Entity" mode="list" />
								</ul>
							</xsl:otherwise>
						</xsl:choose>
					</xsl:template>

				<xsl:template match="Entity" mode="grid">
					<xsl:variable name="Name" select="aspdnsf:GetMLValue(Name)" />
					<xsl:variable name="AltText">
						<xsl:choose>
							<xsl:when test="aspdnsf:GetMLValue(SEAltText)=''">
								<xsl:value-of select="$Name" />
							</xsl:when>
							<xsl:otherwise>
								<xsl:value-of select="aspdnsf:GetMLValue(SEAltText)" />
							</xsl:otherwise>
						</xsl:choose>
					</xsl:variable>
					<xsl:variable name="URL">
						<xsl:value-of select="aspdnsf:EntityLink(EntityID, SEName, $EntityName, 0, '')" />
					</xsl:variable>

					<xsl:if test="position() mod 4 = 1 and position() &gt; 1">
						<xsl:variable name="NewRowHtml">
							<xsl:text>&lt;/div&gt;&lt;div class="row  sub-entity-row"&gt;</xsl:text>
						</xsl:variable>
						<xsl:value-of select="$NewRowHtml" disable-output-escaping="yes" />
					</xsl:if>
					<div class="col-xs-6 col-sm-3 sub-entity-grid-item grid-item">
						<div class="sub-entity-image-wrap grid-item-image-wrap">
							<a href="{$URL}" class="sub-entity-image-link">
								<img src="{aspdnsf:ImageUrl(EntityID, $EntityName, 'icon', 0)}" alt="{$AltText}" class="img-responsive img-thumbnail sub-entity-grid-image grid-item-image" />
							</a>
						</div>
						<div class="sub-entity-name-wrap grid-item-name-wrap">
							<a href="{$URL}" class="sub-entity-name-link">
								<xsl:value-of select="$Name" disable-output-escaping="yes"/>
							</a>
						</div>
					</div>
					<xsl:if test="position() mod 2 = 0">
						<div class="clearfix visible-xs sub-entity-extra-small-clear"></div>
					</xsl:if>
				</xsl:template>

				<xsl:template match="Entity" mode="list">
					<xsl:variable name="Name" select="aspdnsf:GetMLValue(Name)" />
					<xsl:variable name="URL">
						<xsl:value-of select="aspdnsf:EntityLink(EntityID, SEName, $EntityName, 0, '')" />
					</xsl:variable>
					<li class="sub-entity-list-item">
						<a href="{$URL}" class="sub-entity-name-link">
							<xsl:value-of select="$Name" disable-output-escaping="yes"/>
						</a>
					</li>
				</xsl:template>
			</xsl:stylesheet>
	</PackageTransform>
	<SearchEngineSettings>
		<SectionTitle actionType="transform">
			<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf">
				<xsl:output method="html" omit-xml-declaration="yes" />
				<xsl:variable name="EntityName">
					<xsl:value-of select="/root/QueryString/entitytype" />
				</xsl:variable>
				<xsl:variable name="EntityDisplayName">
					<xsl:value-of select="aspdnsf:StringResource(concat('AppConfig.', $EntityName,'PromptPlural'))" />
				</xsl:variable>
				<xsl:template match="/">
					<xsl:value-of select="$EntityDisplayName" />
				</xsl:template>
			</xsl:stylesheet>
		</SectionTitle>
	</SearchEngineSettings>
</package>
