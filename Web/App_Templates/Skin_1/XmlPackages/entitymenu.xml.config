<?xml version="1.0" standalone="yes" ?>
<!-- ##################################################################################	-->
<!-- Copyright AspDotNetStorefront.com. All Rights Reserved.							-->
<!-- http://www.aspdotnetstorefront.com													-->
<!-- For details on this license please visit the product homepage at the URL above.	-->
<!-- THE ABOVE NOTICE MUST REMAIN INTACT.												-->
<!-- ##################################################################################	-->
<package version="2.1" displayname="Entity Menu" debug="false" includeentityhelper="true">
    <PackageTransform>
        <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
              xmlns:msxsl="urn:schemas-microsoft-com:xslt"
              xmlns:aspdnsf="urn:aspdnsf"
              exclude-result-prefixes="aspdnsf">
            <xsl:output method="html" omit-xml-declaration="yes"/>
			
			<xsl:variable name="MenuEntityType" select="aspdnsf:StrCapitalize(/root/Runtime/entitytype)" />
			<xsl:variable name="CurrentEntityType" select="aspdnsf:StrCapitalize(/root/Runtime/PageType)" />
			<xsl:variable name="CurrentEntityID" select="/root/Runtime/PageID" />
			<xsl:variable name="MaxMenuSize" select="aspdnsf:AppConfig('MaxMenuSize')" />
			<xsl:variable name="UseMaxMenuSize" select="$MaxMenuSize &gt; 0" />
			<xsl:variable name="MaxMenuDepth" select="aspdnsf:AppConfig('MaxMenuLevel')" />
			<xsl:variable name="UseMaxMenuDepth" select="$MaxMenuDepth &gt; 0" />

			<xsl:template match="/">
                <ul class="entity-menu">
                    <xsl:apply-templates select="/root/EntityHelpers/*[name() = $MenuEntityType]/Entity" />
                </ul>
				<xsl:if test="$UseMaxMenuSize">
					<script>
						adnsf$(document).ready(function(){
							adnsf$('.menu-item-hidden').hide();
							adnsf$('.menu-item-more').click(function(event){
								adnsf$(this).closest('.menu-item-more').hide();
								adnsf$(this).closest('ul').children('.menu-item-hidden').show();
								event.preventDefault();
							});
						});
					</script>
				</xsl:if>
            </xsl:template>

			<xsl:template match="Entity">
				<xsl:variable name="CurrentDepth" select="count(ancestor::Entity)+1" />
                <li class="menu-item">
					<xsl:if test="$UseMaxMenuSize and (position() &gt; $MaxMenuSize)">
						<xsl:attribute name="class">menu-item menu-item-hidden</xsl:attribute>
					</xsl:if>
                    <a href="{aspdnsf:EntityLink(EntityID, SEName, $MenuEntityType, 0)}">
						<xsl:if test="($CurrentEntityType = $MenuEntityType) and (EntityID = $CurrentEntityID or descendant::Entity/EntityID = $CurrentEntityID)">
                            <xsl:attribute name="class">selected</xsl:attribute>
                        </xsl:if>
						<xsl:value-of select="aspdnsf:GetMLValue(Name)"/>
					</a>
					<!-- 
					If we are on a category and this is a category menu, 
					and if the category id the user is looking at is the same as this one(or one of its childeren), 
					and we have childeren
					and we are at or below the maximum menu depth
					-->
                    <xsl:if test="($CurrentEntityType = $MenuEntityType) and (EntityID = $CurrentEntityID or descendant::Entity/EntityID = $CurrentEntityID) and (count(child::Entity) &gt; 0) and ($UseMaxMenuDepth) and ($CurrentDepth &lt; $MaxMenuDepth)">
                        <ul>
                            <xsl:apply-templates select="Entity" />
                        </ul>
                    </xsl:if>
                </li>
				<xsl:if test="$UseMaxMenuSize and (position() = $MaxMenuSize) and (position() != last())">
					<li class="menu-item-more">
						<a href="#" class="menu-item-more">
							<xsl:value-of select="aspdnsf:StringResource('common.cs.62')" disable-output-escaping="yes" />
						</a>
					</li>
				</xsl:if>
            </xsl:template>

        </xsl:stylesheet>
    </PackageTransform>
</package>
