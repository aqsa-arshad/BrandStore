<?xml version="1.0" standalone="yes" ?>
<!-- ##################################################################################	-->
<!-- Copyright AspDotNetStorefront.com. All Rights Reserved.							-->
<!-- http://www.aspdotnetstorefront.com													-->
<!-- For details on this license please visit the product homepage at the URL above.	-->
<!-- THE ABOVE NOTICE MUST REMAIN INTACT.												-->
<!-- ##################################################################################	-->
<package version="2.1" displayname="Mobile Locale Dropdown" debug="false" includeentityhelper="false">

  <query name="Locales" rowElementName="Locale">
    <sql>
      <![CDATA[
	        select distinct ls.name as localename, ls.[Description] as localedescription, ls.displayorder, case when mobileexclusion.mobilelocale is null then 0 else 1 end as ismobile
          from localesetting ls
	          left join MobileLocaleMapping mobileexclusion on ls.name = mobileexclusion.mobilelocale
          order by ls.displayorder, ls.name   
        ]]>
    </sql>
  </query>

  <PackageTransform>
		<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
			<xsl:output method="html" omit-xml-declaration="yes" />

      <xsl:param name="Type" select="/root/Runtime/type" />
      <xsl:param name="MobileLocales" select="/root/Locales/Locale[ismobile=1]" />
      <xsl:param name="DesktopLocales" select="/root/Locales/Locale[ismobile=0]" />

      <xsl:template match="/">
        <xsl:choose>
          <xsl:when test="aspdnsf:ToLower($Type) != 'desktop'">
            <xsl:call-template name="Mobile" />
          </xsl:when>
          <xsl:otherwise>
            <xsl:call-template name="Desktop" />
          </xsl:otherwise>
        </xsl:choose>
      </xsl:template>

      <xsl:template name="Mobile">
        <xsl:choose>
          <xsl:when test="count($MobileLocales) > 1">
            <ul data-role="listview">
              <li style="padding:3px;">
                <xsl:call-template name="DropDown">
                  <xsl:with-param name="Locales" select="$MobileLocales" />
                </xsl:call-template>
              </li>
              <li class="group" data-role="list-divider"></li>
            </ul>
          </xsl:when>
          <xsl:when test="count($MobileLocales) &lt; 1 and count($DesktopLocales) &gt; 1">
            <ul data-role="listview">
              <li style="padding:3px;">
                <xsl:call-template name="DropDown">
                  <xsl:with-param name="Locales" select="$DesktopLocales" />
                </xsl:call-template>
              </li>
              <li class="group" data-role="list-divider"></li>
            </ul>
          </xsl:when>
        </xsl:choose>
      </xsl:template>

      <xsl:template name="Desktop">
        <xsl:if test="count($DesktopLocales) > 1">
          <xsl:call-template name="DropDown">
            <xsl:with-param name="Locales" select="$DesktopLocales" />
          </xsl:call-template>
        </xsl:if>
      </xsl:template>

      <xsl:template name="DropDown">
        <xsl:param name="Locales" />
        <!-- COUNTRY SELECT LIST -->
        <select size="1" onChange="self.location='setlocale.aspx?returnURL=default.aspx&amp;localesetting=' + document.getElementById('CountrySelectList').value" id="CountrySelectList" name="CountrySelectList" class="CountrySelectList">
          <xsl:apply-templates select="$Locales"/>
        </select>
        <!-- END COUNTRY SELECT LIST -->
      </xsl:template>

      <xsl:template match="Locale">
        <option value="{localename}">
          <xsl:if test="localename = /root/System/LocaleSetting">
            <xsl:attribute name="selected" />
          </xsl:if>
          <xsl:value-of select="localedescription" />
        </option>
      </xsl:template>

		</xsl:stylesheet>
	</PackageTransform>
</package>
